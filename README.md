
This repo:
generating code:
    - generating_harness_cases: contains a script that, given MAVLINK messages in an xml, can generate a bunch of switch-case cases for increasing code coverage
    - generating_message_encoding: contains a script that, given mavlink xml, generates a bunch of functions that can be used to generate messages of that type to use as seeds for fuzzing

generating_seeds
    - using results from generating_message_encoding and helper functions in function.py and constants in constants.py, generate a bunch of seeds for mavlink fuzzing

 udp_example.c: an example of the test harness using results from generating harness cases


 Mavlink message definitions:


 Actually fuzzing:
 - clone this repo: https://github.com/WUSTL-CSPL/mavlink_fuzz_CSE569
 - place the udp_example.c file with one from this repo or one with a better harness
 - change CMakeLists.txt to the text below
 - run `cmake -Bbuild -H. -DCMAKE_PREFIX_PATH=$(pwd)/../../install`
- run `cmake --build build`
- once built you can fuzz `AFL_SKIP_CPUFREQ=1 AFL_USE_ASAN=1 afl-fuzz -i input -o output -- ./build/udp_example @@`, make sure input is the seeds generated by generating_seeds

Fuzzing with multiple cores 
- running AFL as above runs a single instance on a single core
- can run once per core natively w AFL
- on first instance of fuzzer (master) include `-M fuzzer00`
- on all following include `-S fuzzer01` (incrementing number)
- leave at least a core or two or system may crash (i can run 11 instances seemingly mostly stable)


CmakeLists.txt: 
```
cmake_minimum_required(VERSION 3.13)

project(mavlink)

find_package(Python COMPONENTS Interpreter REQUIRED)

# We automatically install the pip dependencies locally below.
# Therefore, we check whether pip is available here.
execute_process(
    COMMAND ${Python_EXECUTABLE} -m pip -V
    RESULT_VARIABLE EXIT_CODE
    OUTPUT_QUIET
)
if (NOT ${EXIT_CODE} EQUAL 0)
    message(FATAL_ERROR "Python pip not found, pip is required")
endif()

if (NOT MAVLINK_DIALECT)
    set(MAVLINK_DIALECT common)
endif()
message(STATUS "MAVLink dialect: ${MAVLINK_DIALECT}")

if (NOT MAVLINK_VERSION)
    set(MAVLINK_VERSION 2.0)
endif()
message(STATUS "MAVLink version: ${MAVLINK_VERSION}")

set(EXAMPLE_HEADER ${CMAKE_CURRENT_BINARY_DIR}/include/mavlink/${MAVLINK_DIALECT}/mavlink.h)

add_custom_command(OUTPUT ${EXAMPLE_HEADER}
    COMMAND ${Python_EXECUTABLE}
        -m pip install -r pymavlink/requirements.txt --upgrade -t ${CMAKE_CURRENT_BINARY_DIR}/pip-dependencies/
        COMMAND ${CMAKE_COMMAND} -E env "PYTHONPATH=${CMAKE_CURRENT_BINARY_DIR}/pip-dependencies/" ${Python_EXECUTABLE}
        -m pymavlink.tools.mavgen
        --lang=C
        --wire-protocol=${MAVLINK_VERSION}
        --output ${CMAKE_CURRENT_BINARY_DIR}/include/mavlink/
        message_definitions/v1.0/${MAVLINK_DIALECT}.xml
        WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
    DEPENDS message_definitions/v1.0/${MAVLINK_DIALECT}.xml
    COMMENT "Generating C headers")

# Unfortunately, the dependencies don't work for INTERFACE libraries.
# The only way I could make it work is to add ALL which means it
# will do the file generation every time even when nothing has changed.
add_custom_target(generate_c_headers
    ALL
    DEPENDS ${EXAMPLE_HEADER})

add_library(mavlink INTERFACE)

add_dependencies(mavlink generate_c_headers)

include(GNUInstallDirs)

target_include_directories(mavlink
    INTERFACE
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}>
)

install(TARGETS mavlink
    EXPORT MAVLinkTargets
    PUBLIC_HEADER DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}
)

install(DIRECTORY "${CMAKE_CURRENT_BINARY_DIR}/include/mavlink"
    DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}"
    FILES_MATCHING PATTERN "*.h"
)

install(EXPORT MAVLinkTargets
    FILE MAVLinkTargets.cmake
    NAMESPACE MAVLink::
    DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/MAVLink
)

# For the build tree
configure_file(MAVLinkConfig.cmake.in
    "${PROJECT_BINARY_DIR}/MAVLinkConfig.cmake" @ONLY)
# For the install tree
configure_file(MAVLinkConfig.cmake.in
    "${PROJECT_BINARY_DIR}${CMAKE_FILES_DIRECTORY}/MAVLinkConfig.cmake" @ONLY)

install(FILES
    "${PROJECT_BINARY_DIR}/${CMAKE_FILES_DIRECTORY}/MAVLinkConfig.cmake"
    DESTINATION ${CMAKE_INSTALL_LIBDIR}/cmake/MAVLink
)
```

